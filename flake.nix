{
  description = "Personal NixOS configs";

  inputs = {
    nixpkgs.url = "nixpkgs/nixos-unstable";

    affinity-nix.url = "github:mrshmllow/affinity-nix";

    apple-fonts = {
      url = "github:Lyndeno/apple-fonts.nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    hyprland.url = "github:hyprwm/Hyprland";

    rose-pine-hyprcursor = {
      url = "github:ndom91/rose-pine-hyprcursor";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.hyprlang.follows = "hyprland/hyprlang";
    };

    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # pretty SDDM theme: https://github.com/uiriansan/SilentSDDM
    silentSDDM = {
      url = "github:uiriansan/SilentSDDM";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    stylix = {
      url = "github:danth/stylix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    nix-flatpak.url = "github:gmodena/nix-flatpak";

    nixos-cosmic.url = "github:lilyinstarlight/nixos-cosmic";

    nix-darwin = {
      url = "github:LnL7/nix-darwin/master";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    spicetify-nix.url = "github:Gerg-L/spicetify-nix";

    vscode-server.url = "github:nix-community/nixos-vscode-server";

    flake-utils.url = "github:numtide/flake-utils";

    quickshell = {
      url = "git+https://git.outfoxxed.me/quickshell/quickshell";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    lanzaboote = {
      url = "github:nix-community/lanzaboote";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    zen-browser = {
      url = "github:0xc000022070/zen-browser-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      home-manager,
      nix-flatpak,
      nixos-cosmic,
      nix-darwin,
      vscode-server,
      stylix,
      flake-utils,
      lanzaboote,
      disko,
      ...
    }@inputs:
    let
      lib = nixpkgs.lib;
    in
    {
      nixosConfigurations =
        let
          mkNixosConfig =
            name:
            {
              system ? "x86_64-linux",
              useDefaultModules ? true,
              enableHomeManager ? true,
              enableFlatpak ? true,
              enableStylix ? true,
              enableVscodeServer ? false,
              enableLanzaboote ? false,
              enableDisko ? false,
              extraModules ? [ ],
            }:
            nixpkgs.lib.nixosSystem {
              specialArgs = {
                inherit inputs;
                myPkgs = inputs.self.packages.${system};
              };
              inherit system;
              modules = [
                ./hosts/${name}/configuration.nix
              ]
              ++ lib.optionals enableHomeManager [
                home-manager.nixosModules.home-manager
                {
                  home-manager.extraSpecialArgs = {
                    inherit inputs;
                    myPkgs = inputs.self.packages.${system};
                  };
                  home-manager.useGlobalPkgs = true;
                  home-manager.useUserPackages = true;
                  home-manager.users.jade = {
                    imports = [
                      ./hosts/${name}/home.nix
                    ];
                  };
                  home-manager.backupFileExtension = "backup";
                }
              ]
              ++ lib.optional useDefaultModules ./modules/nixos
              ++ lib.optional enableFlatpak nix-flatpak.nixosModules.nix-flatpak
              ++ lib.optional enableStylix stylix.nixosModules.stylix
              ++ lib.optional enableVscodeServer vscode-server.nixosModules.default
              ++ lib.optional enableLanzaboote lanzaboote.nixosModules.lanzaboote
              ++ lib.optional enableDisko disko.nixosModules.default
              ++ extraModules;
            };
        in
        {
          desktop = mkNixosConfig "desktop" {
            enableLanzaboote = true;
            extraModules = [
              {
                nix.settings = {
                  substituters = [ "https://cache.garnix.io" ];
                  trusted-public-keys = [ "cache.garnix.io:CTFPyKSLcx5RMJKfLo5EEPUObbA78b0YQ2DTCJXqr9g=" ];
                };
              }
            ];
          };
          game-servers = mkNixosConfig "game-servers" { enableVscodeServer = true; };
          media = mkNixosConfig "media" { enableVscodeServer = true; };
          # mediapi = mkNixosConfig "mediapi" {
          #   system = "aarch64-linux";
          #   enableFlatpak = false;
          #   enableDisko = true;
          #   extraModules = [
          #     ./hosts/mediapi/hardware-configuration.nix # Generated by nixos-anywhere
          #   ];
          # };
          /*
            Cosmic stuff is adapted from FelixSchausberger's Cosmic configuration
            https://github.com/FelixSchausberger/nixos
          */
          t480 = mkNixosConfig "t480" {
            extraModules = [
              {
                nix.settings = {
                  substituters = [ "https://cosmic.cachix.org/" ];
                  trusted-public-keys = [ "cosmic.cachix.org-1:Dya9IyXD4xdBehWjrkPv6rtxpmMdRel02smYzA85dPE=" ];
                };
              }
              nixos-cosmic.nixosModules.default
            ];
          };
        };

      # MacBook configuration
      # darwinConfigurations."macbook" = nix-darwin.lib.darwinSystem {
      #   specialArgs = { inherit inputs; };
      #   modules = [
      #     ./hosts/macbook/configuration.nix
      #     # stylix.nixosModules.stylix
      #     # nix-flatpak.nixosModules.nix-flatpak
      #     # home-manager.nixosModules.home-manager
      #     # {
      #     #   home-manager.extraSpecialArgs = { inherit inputs; };
      #     #   home-manager.useGlobalPkgs = true;
      #     #   home-manager.useUserPackages = true;
      #     #   home-manager.users.jade = {
      #     #     imports = [
      #     #       ./hosts/macbook/home.nix
      #     #     ];
      #     #   };
      #     #   home-manager.backupFileExtension = "backup";
      #     # }
      #   ];
      # };

      homeModules.default = ./modules/hm;
    }
    // flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs {
          inherit system;
        };

        onlyDrvs = attrs: builtins.attrValues (lib.filterAttrs (_: v: lib.isDerivation v) attrs);

        py = pkgs.python313;
        pypkgs = py.pkgs;

        fluent-emoji = lib.recurseIntoAttrs (
          pkgs.callPackage ./pkgs/fluent-emoji-webfont { inherit pkgs; }
        );
        fluent-emoji-all = pkgs.symlinkJoin {
          name = "fluent-emoji-webfont-all";
          paths = onlyDrvs fluent-emoji;
        };

        shell-scripts = lib.recurseIntoAttrs (pkgs.callPackage ./pkgs/shell-scripts { inherit pkgs; });
        shell-scripts-all = pkgs.symlinkJoin {
          name = "shell-scripts-all";
          paths = onlyDrvs shell-scripts;
        };
      in
      {
        devShells.default = pkgs.mkShell {
          # buildInputs = with pkgs; [

          # ];
          # nativeBuildInputs = with pkgs; [
          #   ruff
          #   basedpyright

          #   gtk3
          #   gtk-layer-shell
          #   cairo
          #   gobject-introspection
          #   libdbusmenu-gtk3
          #   gdk-pixbuf
          #   gnome-bluetooth
          #   cinnamon-desktop
          #   (py.withPackages (
          #     ps: with ps; [
          #       setuptools
          #       wheel
          #       build
          #       (inputs.self.packages.${system}.python-fabric)
          #     ]
          #   ))
          #   kdePackages.qtdeclarative
          # ];
        };

        packages = lib.recurseIntoAttrs {
          rofi-bookmarks-zen = pkgs.callPackage ./pkgs/rofi-bookmarks-zen/derivation.nix {
            inherit pypkgs pkgs;
          };

          fluent-emoji-webfont = fluent-emoji;
          fluent-emoji-webfont-all = fluent-emoji-all;

          shell-scripts = shell-scripts;
          shell-scripts-all = shell-scripts-all;
        };

        apps = {
          rofi-bookmarks-zen = {
            type = "app";
            program = "${self.packages.${system}.rofi-bookmarks-zen}/bin/main";
            meta = {
              changelog = "";
              description = "Implementation for grabbing Zen bookmarks in rofi.";
            };
          };
        };
      }
    );
}
